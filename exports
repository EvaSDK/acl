# Copyright (C) 2003  Andreas Gruenbacher <agruen@suse.de>
#
# This program is free software: you can redistribute it and/or modify it
# under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#

ACL_1.0 {
    global:
	# POSIX 1003.1e draft stardard 17 functions
	acl_init;
	acl_dup;
	acl_free;
	acl_valid;
	acl_copy_entry;
	acl_create_entry;
	acl_delete_entry;
	acl_get_entry;
	acl_add_perm;
	acl_calc_mask;
	acl_clear_perms;
	acl_delete_perm;
	acl_get_permset;
	acl_set_permset;
	acl_get_qualifier;
	acl_get_tag_type;
	acl_set_qualifier;
	acl_set_tag_type;
	acl_copy_ext;
	acl_copy_int;
	acl_from_text;
	acl_size;
	acl_to_text;
	acl_delete_def_file;
	acl_get_fd;
	acl_get_file;
	acl_set_fd;
	acl_set_file;

	# Linux specific extensions
	acl_check;
	acl_cmp;
	acl_entries;
	acl_equiv_mode;
	acl_error;
	acl_extended_fd;
	acl_extended_file;
	acl_from_mode;
	acl_get_perm;
	acl_to_any_text;

    local:
    	# Library internal stuff
	__new_var_obj_p;
	__new_obj_p_here;
	__free_obj_p;
	__check_obj_p;
	__ext2int_and_check;
	__acl_reorder_entry_obj_p;
	__acl_reorder_obj_p;
	__acl_init_obj;
	__acl_create_entry_obj;
	__acl_free_acl_obj;
	__acl_to_any_text;
	__apply_mask_to_mode;

	quote;
	unquote;
};

ACL_1.1 {
    global:
	# Linux specific extensions
	perm_copy_fd;
	perm_copy_file;
} ACL_1.0;

ACL_1.2 {
    global:
	# Linux specific extensions
	acl_extended_file_nofollow;

	# NFSv4 specific extensions
	acl_nfs4_add_ace;
	acl_nfs4_add_pair;
	acl_nfs4_free;
	acl_nfs4_new;
	acl_nfs4_set_dir;
	acl_nfs4_set_who;
	acl_nfs4_copy_acl;
	acl_nfs4_xattr_load;
	acl_nfs4_xattr_pack;
	acl_nfs4_xattr_size;
	acl_nfs4_remove_ace;

	acl_n4tp_acl_trans;
	acl_n4tp_set_mode;
	acl_n4tp_ace_count;
	acl_n4tp_ace_trans;
	acl_n4tp_set_who;
	acl_n4tp_get_whotype;

	acl_ptn4_get_mask;
	acl_ptn4_acl_trans;

	acl_nfs4_get_next_ace;
	acl_nfs4_get_first_ace;
	acl_nfs4_get_dir;
	acl_nfs4_get_whotype;
	acl_nfs4_get_who;
	acl_nfs4_entries;

    local:
	__posix_acl_from_nfs4_xattr;
	complementary_ace_pair;
	same_who;
	nfs4_get_gid_from_who;
	nfs4_get_uid_from_who;
	nfs4_get_who_from_uid;
	nfs4_get_who_from_gid;
	__nfs4_get_local_uid_from_who;
	__nfs4_get_foreign_uid_from_who;
	__nfs4_get_local_gid_from_who;
	__nfs4_get_foreign_gid_from_who;
	is_who_local;

	user_obj_from_v4;
	users_from_v4;
	group_obj_and_groups_from_v4;
	mask_from_v4;
	other_from_v4;
} ACL_1.1;
